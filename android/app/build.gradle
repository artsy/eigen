apply plugin: "com.android.application"
apply plugin: "org.jetbrains.kotlin.android"
apply plugin: "com.google.gms.google-services"
apply plugin: "com.facebook.react"
apply plugin: "org.jetbrains.kotlin.android"

project.ext.keyFiles = [
  debug: "keys.shared.json",
  release: "keys.shared.json",
  beta: "keys.shared.json",
]

apply from: project(':react-native-keys').projectDir.getPath() + "/RNKeys.gradle"

def getRuntimeVersion() {
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'node', '../../scripts/deploys/expo-updates/get-runtime-version.js'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}
println "âœ… [Gradle] Using expo_runtime_version: ${runtimeVersion}"
/**
 * This is the configuration block to customize your React Native Android app.
 * By default you don't need to apply any configuration, just uncomment the lines you need.
 */
react {
    /* Folders */
    //   The root of your project, i.e. where "package.json" lives. Default is '../..'
    // root = file("../../")
    //   The folder where the react-native NPM package is. Default is ../../node_modules/react-native
    // reactNativeDir = file("../../node_modules/react-native")
    //   The folder where the react-native Codegen package is. Default is ../../node_modules/@react-native/codegen
    // codegenDir = file("../../node_modules/@react-native/codegen")
    //   The cli.js file which is the React Native CLI entrypoint. Default is ../../node_modules/react-native/cli.js
    // cliFile = file("../../node_modules/react-native/cli.js")

    /* Variants */
    //   The list of variants to that are debuggable. For those we're going to
    //   skip the bundling of the JS bundle and the assets. By default is just 'debug'.
    //   If you add flavors like lite, prod, etc. you'll have to list your debuggableVariants.
    // debuggableVariants = ["liteDebug", "prodDebug"]

    /* Bundling */
    //   A list containing the node command and its flags. Default is just 'node'.
    // nodeExecutableAndArgs = ["node"]
    //
    //   The command to run when bundling. By default is 'bundle'
    // bundleCommand = "ram-bundle"
    //
    //   The path to the CLI configuration file. Default is empty.
    // bundleConfig = file(../rn-cli.config.js)
    //
    //   The name of the generated asset file containing your JS bundle
    // bundleAssetName = "MyApplication.android.bundle"
    //
    //   The entry file for bundle generation. Default is 'index.android.js' or 'index.js'
    // entryFile = file("../js/MyApplication.android.js")
    //
    //   A list of extra flags to pass to the 'bundle' commands.
    //   See https://github.com/react-native-community/cli/blob/main/docs/commands.md#bundle
    // extraPackagerArgs = []

    /* Hermes Commands */
    //   The hermes compiler command to run. By default it is 'hermesc'
    // hermesCommand = "$rootDir/my-custom-hermesc/bin/hermesc"
    //
    //   The list of flags to pass to the Hermes compiler. By default is "-O", "-output-source-map"
    // hermesFlags = ["-O", "-output-source-map"]
    // Override `hermesEnabled` by `expo.jsEngine`
    ext {
        hermesEnabled = (findProperty('expo.jsEngine') ?: "hermes") == "hermes"
    }

    /* Autolinking */
    autolinkLibrariesWithApp()
    //
    // Added by install-expo-modules
    entryFile = file(["node", "-e", "require('expo/scripts/resolveAppEntry')", rootDir.getAbsoluteFile().getParentFile().getAbsolutePath(), "android", "absolute"].execute(null, rootDir).text.trim())
    cliFile = new File(["node", "--print", "require.resolve('@expo/cli')"].execute(null, rootDir).text.trim())
    bundleCommand = "export:embed"
}

/**
 * Set this to true to Run Proguard on Release builds to minify the Java bytecode.
 */
def enableProguardInReleaseBuilds = false

/**
 * The preferred build flavor of JavaScriptCore (JSC)
 *
 * For example, to use the international variant, you can use:
 * `def jscFlavor = 'org.webkit:android-jsc-intl:+'`
 *
 * The international variant includes ICU i18n library and necessary data
 * allowing to use e.g. `Date.toLocaleString` and `String.localeCompare` that
 * give correct results when using with locales other than en-US. Note that
 * this variant is about 6MiB larger per architecture than default.
 */
 def jscFlavor = 'org.webkit:android-jsc:+'

android {
    ndkVersion rootProject.ext.ndkVersion

    buildToolsVersion rootProject.ext.buildToolsVersion
    compileSdk rootProject.ext.compileSdkVersion

    namespace "net.artsy.app"
    defaultConfig {
        applicationId "net.artsy.app"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 7
        versionName "1.0.0"
        vectorDrawables.useSupportLibrary = true

        multiDexEnabled true

        buildConfigField "String", "GITCommitShortHash", '"c77772ee1"'
        buildConfigField "String", "GITCommitHash", '"c77772ee1f32093109049504f7865f9737ad8474"'
        buildConfigField "String", "GITCommitDate", '"2021-01-21T15:08:22+04:00"'
        buildConfigField "String", "GITRemoteOriginURL", '"https://github.com/artsy/eigen.git"'
    }

    signingConfigs {
        debug {
            storeFile file('debug.keystore')
            storePassword 'android'
            keyAlias 'androiddebugkey'
            keyPassword 'android'
        }
        release {
            storeFile file('release.keystore')
            storePassword System.getenv('ANDROID_KEYSTORE_PASSWORD')
            keyAlias 'release'
            keyPassword System.getenv('ANDROID_KEY_PASSWORD')
        }
    }
    buildTypes {
        debug {
            signingConfig signingConfigs.debug
            resValue "string", "expo_runtime_version", "\"${getRuntimeVersion()}\""
            buildConfigField "boolean", "IS_BETA", "false"

            manifestPlaceholders = [
                expoUpdatesEnabled: "false",
                expoUpdatesCheckOnLaunch: "NEVER",
                expoUpdatesChannel: "canary",
                expoRuntimeVersion: getRuntimeVersion(),
                expoUpdatesDisableAntibrickingMeasures: "false"
            ]
        }
        release {
            // Caution! In production, you need to generate your own keystore file.
            // see https://reactnative.dev/docs/signed-apk-android.
            signingConfig signingConfigs.release
            minifyEnabled enableProguardInReleaseBuilds
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
            resValue "string", "expo_runtime_version", "\"${getRuntimeVersion()}\""
            buildConfigField "boolean", "IS_BETA", "false"

            manifestPlaceholders = [
                expoUpdatesEnabled: "true",
                expoUpdatesCheckOnLaunch: "ALWAYS",
                expoUpdatesChannel: "production",
                expoRuntimeVersion: getRuntimeVersion(),
                expoUpdatesDisableAntibrickingMeasures: "false"
            ]
        }
        beta {
            initWith release
            signingConfig signingConfigs.release
            matchingFallbacks = ['release']
            buildConfigField "boolean", "IS_BETA", "true"
            resValue "string", "expo_runtime_version", "\"${getRuntimeVersion()}\""

            manifestPlaceholders = [
                expoUpdatesEnabled: "true",
                expoUpdatesCheckOnLaunch: "NEVER",
                expoUpdatesChannel: "canary",
                expoRuntimeVersion: getRuntimeVersion(),
                expoUpdatesDisableAntibrickingMeasures: "true"
            ]
        }
    }
}

dependencies {
    // The version of react-native is set by the React Native Gradle Plugin
    implementation("com.facebook.react:react-android")

    // This is required to fix an issue that causes the app to crash on startup
    // This can be removed as soon as we get to a react-native version above 0.66
    // See https://github.com/facebook/react-native/issues/31725#issuecomment-901682370
    api(platform("com.squareup.okhttp3:okhttp-bom:4.9.1"))
    api("com.squareup.okhttp3:okhttp")
    api("com.squareup.okhttp3:logging-interceptor")

    implementation 'com.segment.analytics.android:analytics:4.11.3'
    implementation platform('com.google.firebase:firebase-bom:28.1.0')

    implementation "com.google.firebase:firebase-analytics:19.0.0" // so we can see push notification reports on firebase
    implementation 'com.google.firebase:firebase-messaging:21.1.0'

    implementation 'commons-io:commons-io:2.8.0'

    // For WebP support, including animated WebP
    implementation 'com.facebook.fresco:fresco:2.6.0'
    implementation 'com.facebook.fresco:animated-webp:2.6.0'
    implementation 'com.facebook.fresco:webpsupport:2.6.0'
    implementation 'com.android.support:support-core-utils:24.2.1'


    if (hermesEnabled.toBoolean()) {
        implementation("com.facebook.react:hermes-android")
    } else {
        implementation jscFlavor
    }
}
