buildscript {
    ext {
        googlePlayServicesVersion = "17.0.0"
        firebaseMessagingVersion = "21.1.0" // matching firebaseIidVersion to avoid duplicate class error
        buildToolsVersion = "35.0.0"
        minSdkVersion = 24
        compileSdkVersion = 35
        targetSdkVersion = 34
        firebaseIidVersion = "21.1.0" // Needed for react-native-device-info
        googlePlayServicesAuthVersion = "16.0.1"
        ndkVersion = "26.2.11394342"
        kotlinVersion = findProperty('android.kotlinVersion') ?: '1.9.25'
    }
    repositories {
        google()
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath("com.android.tools.build:gradle")
        classpath("com.facebook.react:react-native-gradle-plugin")
        classpath('com.google.gms:google-services:4.3.8')
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin")
    }
}

plugins {
  id("co.uzzu.dotenv.gradle") version "4.0.0"
}

allprojects {
    repositories {
        maven { url "https://appboy.github.io/appboy-android-sdk/sdk" }
        jcenter()
        maven {
            url 'https://api.mapbox.com/downloads/v2/releases/maven'
            authentication {
                basic(BasicAuthentication)
            }
            credentials {
                username 'mapbox'
                password = env.MAPBOX_DOWNLOAD_TOKEN.value
            }
        }
    }
    
    // Configure ccache for all projects in a more modern compatible way
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << '-Xlint:deprecation'
        }
        
        // Set properties for native builds with ccache
        if (System.env.NDK_CCACHE) {
            logger.lifecycle("Configuring NDK builds with ccache: ${System.env.NDK_CCACHE}")
            
            // Set a project-wide property that can be accessed by all modules
            project.ext.ndkCcachePath = System.env.NDK_CCACHE
        }
    }
}

apply plugin: "com.facebook.react.rootproject"
